package hello
public

	with Data_Model;
	with Deployment;

	data myint
		properties
			Data_Model::Data_Representation => Integer;
	end myint;

	thread send
		features
			o: out event data port myint;
	end send;

	thread implementation send.impl
		properties
			Dispatch_Protocol => Periodic;
			Period => 1000 ms;
			source_language => (C);
			source_text => ("hello.c");
			Compute_Entrypoint_Source_Text => "send";
	end send.impl;

	thread recv
		features
			i: in event data port myint {
						source_text => ("hello.c");
						Compute_Entrypoint_Source_Text => "recv";
					};
	end recv;

	thread implementation recv.impl
		properties
			Dispatch_Protocol => Sporadic;
			Period => 2000 ms;
			Initialize_Entrypoint_Source_Text => "init";
	end recv.impl;

	process proc
	end proc;

	process implementation proc.impl
		subcomponents
			send: thread send.impl;
			recv: thread recv.impl;
		connections
			c: port send.o -> recv.i;
	end proc.impl;

	processor cpu
	end cpu;

	processor implementation cpu.impl
		properties
			Deployment::Execution_Platform => NATIVE;
	end cpu.impl;

	system sys
	end sys;

	system implementation sys.impl
		subcomponents
			proc: process proc.impl;
			cpu: processor cpu.impl;
		properties
			Actual_Processor_Binding => (reference (cpu)) applies to proc;
	end sys.impl;

end hello;
